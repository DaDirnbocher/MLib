apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'de.marcphilipp.nexus-publish'
apply from: "${project.rootDir}/gradle/eclipse.gradle"
import java.nio.file.Files

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'de.mediathekview'
archivesBaseName = "MLib"
version = '3.0.3'

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.0"
    classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.2.0"
  }
}

ext {
    propsFile = file('src/main/resources/version.properties').absoluteFile
    if (!propsFile.exists()) {
        Files.createFile(propsFile.toPath())
    }
}

def loadVersionProperties() {
    Properties props = new Properties()
    props.load(propsFile.newDataInputStream())
    return props
}

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs = ['-Xlint:all']
}

compileTestJava {
    options.encoding = "UTF-8"
    options.compilerArgs = ['-Xlint:all']
}


repositories {
    mavenLocal()
    mavenCentral()
    maven {
       url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

if (project.hasProperty('SONATYPE_USERNAME')) {
    signing {
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
	

	publishing {
	  println 'Uploading Version '+version
	  repositories {
	    maven {
	      def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
	      def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
	      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
	      credentials {
	        username = SONATYPE_USERNAME
	        password = SONATYPE_PASSWORD
	      }
	    }
	  }
	  publications {
	    mavenJava(MavenPublication) {
	      pom {
	        name project.name
	        packaging 'jar'
	        description 'The libary for mediathekview/MediathekView and mediathekview/MServer'
	        url 'https://github.com/mediathekview/' + project.name
		
	        scm {
	          connection 'scm:git:git://github.com/mediathekview/MLib.git'
	          developerConnection 'scm:git:ssh://github.com:mediathekview/MLib.git'
	          url 'http://github.com/mediathekview/MLib/tree/master'
	        }
		
	        licenses {
	          license {
	            name 'GNU General Public License v3.0'
	            url 'https://www.gnu.org/licenses/gpl.html'
	          }
	        }
		
	        developers {
	          developer {
	            id 'Nicklas2751'
	            name 'Nicklas Wiegandt'
	            email 'nicklas@mediathekview.de'
	          }
	          developer {
	            id 'derreisende77'
	            name 'Christian F.'
	            url 'https://github.com/derreisende77'
	          }
	          developer {
	            id 'pidoubleyou'
	            name 'Peter W.'
	            url 'https://github.com/pidoubleyou'
	          }
	          developer {
	            id 'TheSasch'
	            name 'Sascha Wiegandt'
	            url 'https://github.com/thesasch'
	          }
	          developer {
	            id 'alex1702'
	            name 'Alexander Finkh√§user'
	            url 'https://github.com/alex1702'
	          }
	        }
	      }
	    }
	  }
	}
	task myJavadocs(type: Javadoc) {
	  source = sourceSets.main.allJava
	  failOnError = false
	}
	
	task javadocJar(type: Jar) {
	    classifier = 'javadoc'
	    from myJavadocs
	}
	    
	task sourcesJar(type: Jar) {
	    classifier = 'sources'
	    from sourceSets.main.allSource
	}
	
	artifacts {
	    archives javadocJar, sourcesJar
	}
}
build.dependsOn(install)

dependencies {
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    compile 'com.jidesoft:jide-oss:3.6.18'
    compile 'org.tukaani:xz:1.8'
    compile 'com.squareup.okhttp3:okhttp:3.14.2'
    compile 'fm.void.jetm:jetm:1.2.3'
}

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

task updateVersion {
    doLast {
        Properties props = loadVersionProperties()
        def oldVersion = props.getProperty('VERSION')
        String buildDate = new Date().format('dd.MM.yyyy HH:mm:ss')
        if (!oldVersion.equals(project.version)) {
            logger.lifecycle "==mlib======================"
            logger.lifecycle "Version: $project.version"
            logger.lifecycle "Baudatum: $buildDate"
            logger.lifecycle "==mlib======================"
            props.setProperty('VERSION', project.version)
            props.setProperty('DATE', buildDate)
            props.store(propsFile.newWriter(), null)
        }
    }
}
processResources.dependsOn updateVersion

/**
 * <b>You don't have to call this. Travis will do it for you if a new releass (tag) will be build!<b/>
 * Call this so: './gradlew build release -PrepoPw=[PASSWORD]'
 */
task release(dependsOn: 'publish') {
    doLast {
        println 'Released Version '+version
    }
}

/**
 * <b>You don't have to call this. Travis will do it for you if you push to develop!<b/>
 * Call this so: './gradlew build releaseSnapshot -PrepoPw=[PASSWORD]'
 */
task releaseSnapshot(dependsOn: 'publishs') {
    doLast {
        println 'Released Snapshot Version '+version
    }
}

gradle.taskGraph.whenReady {taskGraph ->
    if (taskGraph.hasTask(releaseSnapshot)) {
        version = version+'-SNAPSHOT'
    }
}
